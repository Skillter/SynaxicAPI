plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'dev.skillter'
version = '0.0.1-SNAPSHOT'
description = 'Synaxic is a innovative API Hub for developers'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
    all {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
	compileOnly {
		extendsFrom annotationProcessor
	}
    agent
}

repositories {
	mavenCentral()
}

dependencies {
    agent 'net.bytebuddy:byte-buddy-agent:1.17.7'

	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	
    implementation('org.springframework.boot:spring-boot-starter-data-redis') {
        exclude group: 'io.lettuce', module: 'lettuce-core'
    }

	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // API Documentation (Swagger UI + OpenAPI)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
    // Caffeine for in-memory caching
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.2'
    // Bucket4j for rate limiting
    implementation 'com.bucket4j:bucket4j_jdk17-core:8.15.0'
    implementation 'com.bucket4j:bucket4j_jdk17-redisson:8.15.0'
//    implementation 'com.bucket4j:bucket4j_jdk17-jcache:8.15.0'
//    implementation 'javax.cache:cache-api:1.1.1' // (Removed) because we use bucket4j-redisson

    // Metrics for Prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus:1.15.3'
    // Email validation utilities
    implementation 'commons-validator:commons-validator:1.10.0'
    // DNS querys
    implementation 'dnsjava:dnsjava:3.6.3'
    // Google Guava utilities
    implementation 'com.google.guava:guava:33.4.8-jre'
    // Units of Measurement API (for unit conversions)
    implementation 'tech.units:indriya:2.2.3'
    implementation 'systems.uom:systems-common:2.2'
    // Enhanced testing capabilities
    testImplementation 'io.rest-assured:rest-assured:5.5.6'
    testImplementation 'com.redis:testcontainers-redis:2.2.4'
    // RFC 7807 problem+json for standardized error responses
    implementation 'org.zalando:problem-spring-web:0.29.1'
    // For proper request/response logging with JSON format
    implementation 'net.logstash.logback:logstash-logback-encoder:8.1'
    // For secure API key generation and validation
    implementation 'io.jsonwebtoken:jjwt-api:0.12.7'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.7'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.7'
    // IP Geolocation
    implementation 'com.maxmind.geoip2:geoip2:4.3.1'
    // For Redis connection pooling (recommended for production)
    implementation 'org.apache.commons:commons-pool2:2.12.1'
    // For better Jackson JSON handling with Java 8+ time types
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.19.2'
    // Useful string/collection utilities
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'org.springframework.session:spring-session-data-redis:3.5.2'
    implementation 'org.redisson:redisson-spring-boot-starter:4.0.0'
//    implementation 'org.redisson:redisson:3.51.0' // redundant but harmless as long as the version matches
}

tasks.named('bootJar') {
	mainClass = 'dev.skillter.synaxic.SynaxicApplication'
}

tasks.named('test') {
	useJUnitPlatform()
    inputs.files(configurations.agent)
    jvmArgs "-javaagent:${configurations.agent.singleFile}"
}
